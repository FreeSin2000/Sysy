use crate::ast::*;
// lalrpop 里的约定
grammar;

// 约束 lexer 的行为
match {
  // 跳过空白符和注释
  r"\s*" => {},
  r"//[^\n\r]*[\n\r]*" => {},
  r"/\*([^*]|\*[^/])*\*/" => {},
  // 剩下的情况采用默认方式处理
  _
}

pub CompUnit: CompUnit = <comp_unit: CompUnit?> <global_unit: GlobalUnit> => CompUnit{comp_unit:Box::new(comp_unit), global_unit};

GlobalUnit: GlobalUnit = {
  <func_def: FuncDef> => GlobalUnit::FuncDef(func_def),
  <decl: Decl> => GlobalUnit::Decl(decl),
}
// FuncDef     ::= FuncType IDENT "(" [FuncFParams] ")" Block;
FuncDef: FuncDef = {
  <func_type: CompType> <ident: Ident> "(" <params: FuncFParams?> ")" <block: Block> => {
    FuncDef {func_type: func_type.into(), ident, params, block}
  }
}

// FuncFParams ::= FuncFParam {"," FuncFParam};
FuncFParams: FuncFParams = <params: Comma<FuncFParam>> => FuncFParams{<>};

// FuncFParam  ::= BType IDENT;
FuncFParam: FuncFParam = <param_type: CompType> <param_name: Ident> => FuncFParam{param_type: param_type.into(), param_name};

// FuncRParams ::= Exp {"," Exp};
FuncRParams: FuncRParams = <params: Comma<Exp>> => FuncRParams{params};

// Block         ::= "{" {BlockItem} "}";
// Block: Block = "{" <stmt: Stmt> "}" => Block { <> };
Block: Block = {
  "{" <block_items: BlockItem*> "}" => Block{<>}
}
// Stmt          ::= LVal "=" Exp ";"
//                 | "return" Exp ";";
Stmt: Stmt = {
  <stmt: OpenIfStmt> => <>,
  <stmt: ClosedIfStmt> => <>,
}

NoIfStmt: Stmt = {
  "return" <exp: Exp?> ";" => Stmt::RetExp(<>),
  <opt_exp: Exp?> ";" => Stmt::OptExp(<>),
  <block: Block> => Stmt::Block(<>),
  <lval: LVal> "=" <exp: Exp> ";" => Stmt::LValExp(lval, exp),
  "continue" ";" => Stmt::ContStmt,
  "break" ";" => Stmt::BreakStmt,
}

OpenIfStmt: Stmt = {
  "if" "(" <exp: Exp> ")" <then_stmt: Stmt> => Stmt::IfStmt(exp, Box::new(then_stmt), None),
  "if" "(" <exp: Exp> ")" <then_stmt: ClosedIfStmt> "else" <else_stmt: OpenIfStmt> => Stmt::IfStmt(exp, Box::new(then_stmt), Some(Box::new(else_stmt))),
  "while" "("<exp: Exp> ")" <stmt: OpenIfStmt> => Stmt::WhileStmt(exp, Box::new(stmt)),
}

ClosedIfStmt: Stmt = {
  <stmt: NoIfStmt> => <>,
  "if" "(" <exp: Exp> ")" <then_stmt: ClosedIfStmt> "else" <else_stmt: ClosedIfStmt> => Stmt::IfStmt(exp, Box::new(then_stmt), Some(Box::new(else_stmt))),
  "while" "("<exp: Exp> ")" <stmt: ClosedIfStmt> => Stmt::WhileStmt(exp, Box::new(stmt)),
}

// Exp         ::= LOrExp;
Exp: Exp = <lor_exp: LOrExp> => Exp {lor_exp:Box::new(<>)};

// PrimaryExp    ::= "(" Exp ")" | LVal | Number;
PrimaryExp: PrimaryExp = {
  "(" <exp: Exp> ")" => PrimaryExp::ParenthesizedExp(<>),
  <lval: LVal> => PrimaryExp::LVal(<>),
  <num: Number> => PrimaryExp::Number(<>),
}

Number: Number = {
  <num: IntConst> => Number::INT_CONST(<>),
}

// | IDENT "(" [FuncRParams] ")"
UnaryExp: UnaryExp = {
  <primary_exp: PrimaryExp> => UnaryExp::PrimaryExp(primary_exp),
  <unary_op: "+"> <unary_exp: UnaryExp> => UnaryExp::UnaryOpExp(UnaryOp::Plus, Box::new(unary_exp)),
  <unary_op: "-"> <unary_exp: UnaryExp> => UnaryExp::UnaryOpExp(UnaryOp::Minus, Box::new(unary_exp)),
  <unary_op: "!"> <unary_exp: UnaryExp> => UnaryExp::UnaryOpExp(UnaryOp::Not, Box::new(unary_exp)),
  <ident: Ident> "(" <params: FuncRParams?> ")" => UnaryExp::FuncCall(ident, params),
}

MulExp: MulExp = {
  <unary_exp: UnaryExp> => MulExp::UnaryExp(<>), 
  <mul_exp: MulExp> <mul_op: "*"> <unary_exp: UnaryExp> => MulExp::MulOpExp(Box::new(mul_exp), MulOp::Mul, unary_exp),
  <mul_exp: MulExp> <mul_op: "/"> <unary_exp: UnaryExp> => MulExp::MulOpExp(Box::new(mul_exp), MulOp::Div, unary_exp),
  <mul_exp: MulExp> <mul_op: "%"> <unary_exp: UnaryExp> => MulExp::MulOpExp(Box::new(mul_exp), MulOp::Mod, unary_exp),
}

AddExp: AddExp = {
  <mul_exp: MulExp> => AddExp::MulExp(<>), 
  <add_exp: AddExp> <add_op: "+"> <mul_exp: MulExp> => AddExp::AddOpExp(Box::new(add_exp), AddOp::Add, mul_exp),
  <add_exp: AddExp> <add_op: "-">  <mul_exp: MulExp> => AddExp::AddOpExp(Box::new(add_exp), AddOp::Sub, mul_exp),
}

// RelExp      ::= AddExp | RelExp ("<" | ">" | "<=" | ">=") AddExp;

RelExp: RelExp = {
  <add_exp: AddExp> => RelExp::AddExp(<>),
  <rel_exp: RelExp> "<" <add_exp: AddExp> => RelExp::RelOpExp(Box::new(rel_exp), RelOp::Lt, add_exp),
  <rel_exp: RelExp> ">" <add_exp: AddExp> => RelExp::RelOpExp(Box::new(rel_exp), RelOp::Gt, add_exp),
  <rel_exp: RelExp> "<=" <add_exp: AddExp> => RelExp::RelOpExp(Box::new(rel_exp), RelOp::Le, add_exp),
  <rel_exp: RelExp> ">=" <add_exp: AddExp> => RelExp::RelOpExp(Box::new(rel_exp), RelOp::Ge, add_exp),
}

// EqExp       ::= RelExp | EqExp ("==" | "!=") RelExp;

EqExp: EqExp = {
  <rel_exp: RelExp> => EqExp::RelExp(<>),
  <eq_exp: EqExp> "==" <rel_exp: RelExp> => EqExp::EqOpExp(Box::new(eq_exp), EqOp::Eq, rel_exp),
  <eq_exp: EqExp> "!=" <rel_exp: RelExp> => EqExp::EqOpExp(Box::new(eq_exp), EqOp::NotEq, rel_exp),
}

// LAndExp     ::= EqExp | LAndExp "&&" EqExp;

LAndExp: LAndExp = {
  <eq_exp: EqExp> => LAndExp::EqExp(<>),
  <land_exp: LAndExp> "&&" <eq_exp: EqExp> => LAndExp::LAndOpExp(Box::new(land_exp), eq_exp),
}

// LOrExp      ::= LAndExp | LOrExp "||" LAndExp;
LOrExp: LOrExp = {
  <land_exp: LAndExp> => LOrExp::LAndExp(<>),
  <lor_exp: LOrExp> "||" <land_exp: LAndExp> => LOrExp::LOrOpExp(Box::new(lor_exp), land_exp),
}

// Decl          ::= ConstDecl | VarDecl;
Decl: Decl = {
  <const_decl: ConstDecl> => Decl::ConstDecl(const_decl),
  <var_decl: VarDecl> => Decl::VarDecl(var_decl),
}

// ConstDecl     ::= "const" BType ConstDef {"," ConstDef} ";";

Comma<T>: Vec<T> = {
    <mut v:(<T> ",")*> <e:T> => {
      v.push(e);
      v
    }
};

ConstDecl: ConstDecl = "const" <btype: CompType> <const_defs: Comma<ConstDef>> ";" => ConstDecl{btype: btype.into(), const_defs};

// BType         ::= "int";

CompType:CompType = {
  "int" => CompType::Int,
  "void" => CompType::Void,
}

// ConstDef      ::= IDENT "=" ConstInitVal;
ConstDef: ConstDef = <ident: Ident> "=" <const_init_val: ConstInitVal> => ConstDef{ident, const_init_val};
// ConstInitVal  ::= ConstExp;
ConstInitVal: ConstInitVal = <const_exp: ConstExp> => ConstInitVal{<>};

// BlockItem     ::= Decl | Stmt;
BlockItem: BlockItem = {
  <decl: Decl> => BlockItem::Decl(<>),
  <stmt: Stmt> => BlockItem::Stmt(<>),
}

// LVal          ::= IDENT;
LVal: LVal = <ident: Ident> => LVal::IDENT(<>);

// ConstExp      ::= Exp;
ConstExp: ConstExp = <exp: Exp> => ConstExp { <> };

// InitVal       ::= Exp;
InitVal: InitVal = <exp: Exp> => InitVal{<>};

// VarDef        ::= IDENT | IDENT "=" InitVal;
VarDef: VarDef = {
  <ident: Ident> => VarDef::IDENT(<>),
  <ident: Ident> "=" <init_val: InitVal> => VarDef::IDENTInitVal(ident, init_val),
}

// VarDecl       ::= BType VarDef {"," VarDef} ";";
VarDecl: VarDecl = <btype: CompType> <var_defs: Comma<VarDef>> ";" => VarDecl{btype: btype.into(), var_defs};

// 如果匹配到标识符, 就返回这个字符串
// 一对尖括号在此处指代的是正则表达式匹配到的字符串 (&str)
// 关于尖括号到底代表什么, 请 RTFM
Ident: String = r"[_a-zA-Z][_a-zA-Z0-9]*" => <>.to_string();

// 对整数字面量的处理方式: 把匹配到的字符串按对应进制转换成数字
IntConst: i32 = {
  r"[1-9][0-9]*" => i32::from_str_radix(<>, 10).unwrap(),
  r"0[0-7]*" => i32::from_str_radix(<>, 8).unwrap(),
  r"0[xX][0-9a-fA-F]+" => i32::from_str_radix(&<>[2..], 16).unwrap(),
}
